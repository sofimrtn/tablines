
El primero de los ejemplos muestra como leer "ventanas" de datos de una hoja excel cuando en una misma columna aparecen entidades diferentes que representan algún tipo de anidación.

Ls siguiente excel de ejemplo contiene un parte de horas de empleados de una empresa. En la primera columna 'Rótulos' aparecen los empleados (marcados con '-'), las actividades (empiezan por una letra) y las subactividades (empiezan por un número). En la columnas a la derecha aparecen las horas respectivas de cada mes desde enero a mayo.

!ejemplo1.png!

El objetivo sería recorrer todas las horas registradas asociando cada una a su mes y su alumno, actividad o subactividad correspondiente.

Este sería el código completo del ejemplo:

{code}
PREFIX ex: <http://example.org/ex#>
PREFIX scv: <http://purl.org/NET/scovo#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX scvxl: <http://idi.fundacionctic.org/scovoxl/scovoxl#>

    IN sheets "Hoja1"
        FOR ?employee IN rows FILTER matches(?employee,"-[a-zA-Z0-9ñ]+ [a-zA-Z0-9]*")
            LET ?resourceEmployee = resource(?employee,<http://localhost:8080/tabels-web/pubby/resource/>)
	{
	    FOR ?horasEmployee IN cols starts at 1 RIGHT of ?employee until not matches(?horasEmployee, "[0-9]+")
                Set ?mesEmployee in rows "1" 
                     LET ?resourceMesEmployee = resource( ?mesEmployee, <http://localhost:8080/tabels-web/pubby/resource/>)
             ;
	    FOR ?actividad IN rows FILTER matches(?actividad,"[a-z][a-z0-9]-[a-zA-Z0-9]+[a-zA-Z0-9 ]*")
		LET ?resourceActividad = resource(?actividad,<http://localhost:8080/tabels-web/pubby/resource/>)
                {
                FOR ?horasActividad IN cols starts at 1 RIGHT of ?actividad until not matches(?horasActividad, "[0-9]+")
                     SET ?mesActividad in rows "1"
                     	LET ?resourceMesActividad = resource( ?mesActividad, <http://localhost:8080/tabels-web/pubby/resource/>)
                ;
		FOR ?subactividad IN rows FILTER matches(?subactividad,"[0-9]+-[a-zA-Z0-9]+[a-zA-Z0-9 ]*")
			LET ?resourceSubActividad = resource(?subactividad,<http://localhost:8080/tabels-web/pubby/resource/>)	
                	FOR ?horasSubActividad IN cols starts at 1 RIGHT of ?subactividad until not matches(?horasSubActividad, "[0-9]+")
                        	SET ?mesSubActividad in rows "1"
                                	LET ?resourceMesSubActividad = resource( ?mesSubActividad, <http://localhost:8080/tabels-web/pubby/resource/>)
                 
	  }
         }	
	     
            	
////////////////////DEDICACIÓN EMPLEADO MES//////////////////////////////

//DIMENSION TRABAJADOR
{
    ?resourceEmployee a ex:worker ;
                      skos:prefLabel ?employee .
    ex:worker rdfs:subClassOf scv:Dimension ;
              skos:prefLabel "Trabajador"
    
}
//DIMENSION MES
{
    ?resourceMesEmployee a ex:month ;
                      skos:prefLabel ?mesEmployee .
    ex:month rdfs:subClassOf scv:Dimension ;
              skos:prefLabel "Mes"
    
}
//DATA SET
{
    [ a scv:Item ;
        scv:dataset ex:DedicacionEmpleadoMesDataSet ;
        scv:dimension ?resourceEmployee ;
        rdf:value ?horasEmployee;
        scv:dimension ?resourceMesEmployee]
}
  
{
    ex:DedicacionEmpleadoMesDataSet a scv:Dataset ;
  	 skos:prefLabel "Dedicacion Empleado-Mes" ;
         scvxl:valuesMeasuredIn "Horas"
  
}

/////////////////////DATA SET DEDICACIÓN EMPLEADO ACTIVIDAD MES//////////////////////

{
    ?resourceActividad a ex:actividad ;
                      skos:prefLabel ?actividad .
    ex:actividad rdfs:subClassOf scv:Dimension ;
              skos:prefLabel "Actividad"
    
}
{
    ?resourceMesActividad a ex:month ;
                      skos:prefLabel ?mesActividad .
    ex:month rdfs:subClassOf scv:Dimension ;
              skos:prefLabel "Mes"
    
}
{
    
    [ a scv:Item ;
        scv:dataset ex:DedicacionEmpleadoActividadMesDataSet ;
        scv:dimension ?resourceEmployee ;
        scv:dimension ?resourceActividad ;
        rdf:value ?horasActividad;
        scv:dimension ?resourceMesActividad]
}
  
{
    ex:DedicacionEmpleadoActividadMesDataSet a scv:Dataset ;
                                             skos:prefLabel "Dedicacion Empleado-Actividad-Mes" ;
                                             scvxl:valuesMeasuredIn "Horas"
}


//////////////////DATA SET EMPLEADO SUBACTIVIDAD MES//////////////////////

{
    ?resourceSubActividad a ex:subactividad ;
                      skos:prefLabel ?subactividad .
    ex:subactividad rdfs:subClassOf scv:Dimension ;
              skos:prefLabel "SubActividad"
    
}
{
    ?resourceMesSubActividad a ex:month ;
                      skos:prefLabel ?mesSubActividad .
    ex:month rdfs:subClassOf scv:Dimension ;
              skos:prefLabel "Mes"
    
}
{
    
    [ a scv:Item ;
        scv:dataset ex:DedicacionEmpleadoSubActividadMesDataSet ;
        scv:dimension ?resourceEmployee ;
        scv:dimension ?resourceSubActividad ;
        rdf:value ?horasSubActividad;
        scv:dimension ?resourceMesSubActividad]
}
  
{
    ex:DedicacionEmpleadoSubActividadMesDataSet a scv:Dataset ;
                                             skos:prefLabel "Dedicacion Empleado-SubActividad-Mes" ;
                                             scvxl:valuesMeasuredIn "Horas"
                                                  
}
{code}
